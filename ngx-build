#!/usr/bin/env perl

use strict;
use warnings;

use File::Spec;
use Getopt::Std;

my $cc = $ENV{NGX_BUILD_CC};
my $optimize = $ENV{NGX_BUILD_OPT};
my $jobs = $ENV{NGX_BUILD_JOBS} || 1;
my $disable_no_pool = $ENV{NGX_BUILD_DISABLE_NO_POOL};
my $no_debug = $ENV{NGX_BUILD_NO_DEBUG};

my %opts;
getopts("fp:", \%opts) or usage();

if (@ARGV == 0) {
    usage();
}

my $force = $opts{f};
if ($force) {
    shift;
}

my $patch = $opts{p};
if ($patch) {
    $patch = File::Spec->rel2abs($patch);
}

my $version = shift or
    usage();

if ($version !~ /^(\d+)\.(\d+)\.(\d+)$/) {
    warn "bad nginx version number: $version\n";
    usage();
}

my @ver = ($2, $2, $3);
my $ver = sprintf("%03d%03d%03d", $1, $2, $3);

my @opts;
if (($1 == 1 && $2 == 1 && $3 >= 4)
    || ($1 == 1 && $2 > 1)
    || ($1 > 1))
{
    for my $arg (@ARGV) {
        if ($arg =~ /^--add-module=.*?ngx_http_upstream_keepalive/) {
            next;
        }
        push @opts, $arg;
    }

} else {
    @opts = @ARGV;
}

push @opts, '--with-poll_module';

my $found_cc_opt;
my @new_opts;
for my $opt (@opts) {
    if ($no_debug && $opt eq '--with-debug') {
        next;
    }

    if (defined $cc) {
        if ($opt =~ /^--with-cc=/) {
            next;
        }
    }

    if (defined $optimize) {
        if ($opt =~ /^--with-cc-opt=/) {
            if ($opt =~ s/-O\d?\b/$optimize/g) {
                $found_cc_opt = 1;

            } else {
                $opt .= " $optimize";
            }

            $found_cc_opt = 1;
        }
    }

    push @new_opts, $opt;
}

@opts = @new_opts;

if (defined $cc) {
    push @opts, "--with-cc=$cc";
}

if (defined $optimize && !$found_cc_opt) {
    push @opts, "--with-cc-opt=$optimize";
}

my $root = `pwd`;
chomp $root;
my $target = "$root/work/nginx";

my $home = `cd ~ && pwd`;
chomp $home;

my $build_root = "buildroot";
if (! -d $build_root) {
    mkdir $build_root or
        die "failed to create directory $build_root.\n";
}

chdir $build_root or
    die "failed to switch to directory $build_root\n";

my $tarball = "nginx-$version.tar.gz";
if ($force || ! -f $tarball || ! -d "nginx-$version") {
    if (!-d "$home/work") {
        mkdir "$home/work" or
            die "failed to create directory ~/work\n";
    }

    if (-f "$home/work/$tarball") {
        shell("cp $home/work/$tarball ./");

    } else {
        shell("axel -n2 -o $home/work/$tarball http://nginx.org/download/$tarball");
        shell("cp $home/work/$tarball ./");
    }

    shell("tar -xzvf $tarball");

    apply_patches();
}

chdir "nginx-$version" or die "cannot switch to nginx-$version\n";

if ($force || ! -f "Makefile" || -M "$root/config" < -M "Makefile" ||
        -M "$root/util/build.sh" < -M "Makefile")
{
    configure_nginx();
}

if (-f "$target/logs/nginx.pid") {
    system("kill -QUIT `cat $target/logs/nginx.pid") == 0 or
        warn "failed to quit existing nginx.\n";
}

shell("make -j$jobs");
shell("make install");

sub configure_nginx {
    system("./configure", "--prefix=$target",
        @opts) == 0 or
            die "failed to configure nginx.\n";
}

sub apply_patches {
    if ($patch) {
        shell("patch -p0 < $patch");
    }

    if ($ver lt '001000014' || ($ver ge '001001000' && $ver lt '001001017')) {
        chdir "nginx-$version" or die "cannot switch to nginx-$version\n";
        warn "applying the null char fixes security patch";
        shell("patch -p0 -l < $root/../ngx_openresty/patches/nginx-$version-null_character_fixes.patch");
        chdir "..";
    }

    unless ($ver ge '001001015') {
        chdir "nginx-$version" or die "cannot switch to nginx-$version\n";
        warn "applying subrequest cycle protection patch";
        shell("patch -p1 -l < $root/../ngx_openresty/patches/nginx-$version-subrequest_loop.patch");
        chdir "..";
    }

    warn "applying the upsteram_pipelining patch...\n";
    shell("patch -p0 < $root/../ngx_openresty/patches/nginx-$version-upstream_pipelining.patch");

    unless ($disable_no_pool) {
        warn "applying no-pool patch...\n";
        shell("patch -p0 < $root/../no-pool-nginx/nginx-$version-no_pool.patch");
    }

    warn "applying no-error-page patch...\n";
    shell("patch -p0 < $root/../ngx_openresty/patches/nginx-$version-no_error_pages.patch");

    warn "applying the max_subrequests patch...\n";
    shell("patch -p0 < $root/../ngx_openresty/patches/nginx-$version-max_subrequests.patch");

    unless ($ver ge '001001011' or ($ver ge '001000012' and $ver le '001001000')) {
        shell("patch -p0 < $root/../ngx_openresty/patches/nginx-$version-variable_header_ignore_no_hash.patch");
    }

    shell("patch -p0 < $root/../ngx_openresty/patches/nginx-$version-reset_wev_handler_in_named_locations.patch");

    shell("patch -p0 < $root/../ngx_openresty/patches/nginx-$version-filter_finalize_hang.patch");

    shell("patch -p0 < $root/../ngx_openresty/patches/nginx-$version-add_core_vars_polluting_globals.patch");

    shell("patch -p0 < $root/../ngx_openresty/patches/nginx-$version-resolver_debug_log_overflow.patch");

    shell("patch -p0 < $root/../ngx_openresty/patches/nginx-$version-poll_del_event_at_exit.patch");

    unless ($ver ge '001001006') {
        shell("patch -p0 < $root/../ngx_openresty/patches/nginx-$version-named_location_clear_mods_ctx.patch");
    }

    shell("patch -p0 < $root/../ngx_openresty/patches/nginx-$version-allow_request_body_updating.patch");

    unless ($ver ge '001001009' or ($ver ge '001000012' and $ver le '001001000')) {
        shell("patch -p0 < $root/../ngx_openresty/patches/nginx-$version-epoll_check_stale_wev.patch");
    }
    #shell("patch -p0 < $root/../ngx_openresty/patches/nginx-$version-gzip_ok_invalid_read_fix.patch");

    unless ($ver ge '001000009') {
        shell("patch -p0 < $root/../ngx_openresty/patches/nginx-$version-log_escape_non_ascii.patch");
    }

    unless ($ver ge '001000008') {
        warn "applying request-body-preread patch...\n";
        shell("patch -p0 < $root/../ngx_openresty/patches/nginx-$version-request_body_preread_fix.patch");

        chdir "nginx-$version" or die "cannot switch to nginx-$version\n";
        warn "applying request-body-in-single-buf patch...\n";
        shell("patch -p1 -l < $root/../ngx_openresty/patches/nginx-$version-request_body_in_single_buf.patch");
        chdir ".." or die "cannot switch to ..\n";
    }
}

sub usage {
    die "Usage: $0 <nginx-version> <options>\n";
}

sub shell {
    my $cmd = shift;
    warn "$cmd\n";
    system($cmd) == 0 or
        die "failed to run command \"$cmd\"\n";
}

